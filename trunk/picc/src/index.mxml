<?xml version="1.0" encoding="utf-8"?>
<mx:Application xmlns:mx="http://www.adobe.com/2006/mxml" layout="absolute" creationComplete="appInit()">
	<mx:HTTPService id="httpSvr" url="content.xml" result="resultHandler(event)" resultFormat="xml"/>
	<mx:Style source="skin/skin.css"/>	
	<mx:Script>
		<![CDATA[
			import mx.controls.sliderClasses.SliderThumb;
			import mx.rpc.events.ResultEvent;			
			import component.SliderHighlightSkin;			
			import mx.core.UIComponent;
			import mx.managers.PopUpManager;
			import component.NoteManage;			
			import mx.managers.SystemManager;
			import mx.events.FlexEvent;
			import mx.events.SliderEvent;
			import mx.collections.ArrayCollection;
			import mx.events.ListEvent;
			import mx.containers.GridItem;
			import mx.containers.GridRow;
			import mx.controls.Button;
			import mx.controls.Alert;			
			
			[Bindable]
			private var logo:String;
			[Bindable]
			private var courseNameL:String;
			[Bindable]
			private var menuTitle:String;
			[Bindable]
			private var courseNameS:String;
			[Bindable]
			private var mc:MovieClip;
			[Bindable]
			private var currSectionNum:int;
			[Bindable]
			private var totalSectionNum:int;
			[Bindable]
			private var currChapterName:String;
			[Bindable]
			private var currSectionName:String;			
			
			[Embed(source="skin/btnFocus.swf",symbol="Button_focusSkin")]
			private var focusSkin:Class;
			[Embed(source="skin/btnFocus.swf",symbol="Button_upSkin")]
			private var upSkin:Class;
			[Embed(source="skin/skin.swf",symbol="Button_ksh_disabledSkin")]
			private var ksh_disabledSkin:Class;
			[Embed(source="skin/skin.swf",symbol="Button_ksh_overSkin")]
			private var ksh_overSkin:Class;
			[Embed(source="skin/skin.swf",symbol="Button_ksh_downSkin")]
			private var ksh_downSkin:Class;
			[Embed(source="skin/skin.swf",symbol="Button_ksh_upSkin")]
			private var ksh_upSkin:Class;
			[Bindable]
			[Embed(source="skin/skin.swf",symbol="Button_lb_disabledSkin")]
			private var sy_upSkin:Class;
			[Bindable]
			[Embed(source="skin/skin.swf",symbol="Button_lb_overSkin")]
			private var sy_downSkin:Class;
			[Bindable]
			[Embed(source="skin/skin.swf",symbol="Button_lb_downSkin")]
			private var sy_overSkin:Class;			
			
			[Bindable]
			private var content:XMLList;
			private var menuItemArray1:ArrayCollection=new ArrayCollection();//单页
			private var menuItemArray2:ArrayCollection;//多页 
			private var soundTrans:SoundTransform=SoundMixer.soundTransform;
			private var timer:Timer=new Timer(60);
			private var xml:String;
			private var app:Object;
			private var lso:SharedObject;
			private var isContinue:Boolean;
			private var lastCourseInfo:Object=new Object();			
			private var noteManage:NoteManage;
			private var lsoInfo:Object;
			public var isPopup:Boolean=false;
			private var lastBtn:Button;
			
			private function appInit():void
			{
				httpSvr.send();
				Alert.okLabel="确定";
				Alert.yesLabel="是";
				Alert.noLabel="否";	
				timer.addEventListener(TimerEvent.TIMER,timerHandler);
				//注册回调函数,让外部js调用
				/* if(ExternalInterface.available)
				{
                	try
					{
						ExternalInterface.addCallback("writeLog",writeLog);						
					}
					catch(error:SecurityError)
					{
						Alert.show("给外部js添加回调函数时出错,存在安全沙箱问题!\n"+error.message,"错误提示");
					}
           	 	}
           	 	else
           	 	{
           	 		Alert.show("External interface is not available for this container!");
           	 	}		 */
			}
			
			private function resultHandler(event:ResultEvent):void
			{				
				content=XMLList(event.result);				
				this.currentState="header";					
			}
			
			private function init():void
			{
				this.currentState="main";
				//课程菜单初始化
				for(var i:int=0;i<8;i++)
				{//8行3列
					var gridRow:GridRow=new GridRow();
					gridMenu.addChildAt(gridRow,i);
					for(var j:int=0;j<3;j++)
					{
						var gridItem:GridItem=new GridItem();
						GridRow(gridMenu.getChildAt(i)).addChildAt(gridItem,j);
					}
				}
				var count:int=0;
				var firstBtn:Button;	
				for each(var item:XML in content.MenuItems.menuItem)
				{
					var btn:Button=new Button();
					btn.width=41;
					btn.height=41;	
					btn.styleName="menuButton";							
					btn.label=item.@label;	
					var fullName:String;
					if(item.@fullName==null||item.@fullName.length==0)
					{
						fullName=item.@label;
					}
					else
					{
						fullName=item.@fullName;
					}
					if(item.hasOwnProperty("@isMultipage")&&item.@isMultipage==true)
					{	
						btn.data={fullName:fullName,data:item.@data,xml:item.@xml};						
					}
					else
					{
						btn.data={fullName:fullName,data:item.@data};
					}
					btn.addEventListener(MouseEvent.CLICK,onMenuClick);
					//btn.addEventListener(FocusEvent.FOCUS_IN,onBtnFocusIn);
					//btn.addEventListener(FocusEvent.FOCUS_OUT,onBtnFocusOut);
					if(count==0)
					{
						firstBtn=btn;
					}	
					var row:int=item.@row;
					var col:int=item.@col;					
					GridItem(GridRow(gridMenu.getChildAt(row-1)).getChildAt(col-1)).addChild(btn);
					menuItemArray1.addItem({count:++count,button:btn});
				}
				for each(var btnItem:XML in content.EffectButton.button)
				{
					var img:Image=new Image();
					img.width=41;
					img.height=41;
					img.load(btnItem.@swf);
					GridItem(GridRow(gridMenu.getChildAt(btnItem.@row-1)).getChildAt(btnItem.@col-1)).addChild(img);	
				}
				this.courseNameL=content.Banners.courseNameL.@data;
				this.logo=content.Banners.logo.@data;
				this.menuTitle=content.Banners.menuTitle.@data;
				this.courseNameS=content.Banners.courseNameS.@data;
				lso=SharedObject.getLocal("picc/lastCourseInfo","/");				
				if(lso==null)
				{
					Alert.show("无法创建或获取本地共享对象!","Error");
					firstBtn.dispatchEvent(new MouseEvent(MouseEvent.CLICK));
					//firstBtn.dispatchEvent(new FocusEvent(FocusEvent.FOCUS_IN));
				}
				else
				{
					if(isContinue && lso.data.info!=null && lso.data.info!=undefined)
					{
						lsoInfo=lso.data.info;
						if(lsoInfo.isMultipage)
						{	
							for each(var arrayItem:Object in menuItemArray1)
							{
								if(arrayItem.button.data.fullName==lsoInfo.currChapterName)
								{
									Button(menuItemArray1[arrayItem.count-1].button).dispatchEvent(new MouseEvent(MouseEvent.CLICK));
									//firstBtn.dispatchEvent(new FocusEvent(FocusEvent.FOCUS_IN));
									break;						
								}
							}							 
						}
						else
						{
							Button(menuItemArray1[lsoInfo.currSectionNum-1].button).dispatchEvent(new MouseEvent(MouseEvent.CLICK));
							//firstBtn.dispatchEvent(new FocusEvent(FocusEvent.FOCUS_IN));
						}
					}
					else
					{		
						if(isContinue)
						{
							isContinue=false;							
						}										
						firstBtn.dispatchEvent(new MouseEvent(MouseEvent.CLICK));
						//firstBtn.dispatchEvent(new FocusEvent(FocusEvent.FOCUS_IN));
					}
					lso.data.info=lastCourseInfo;	
				}
			}
			
			private function timerHandler(evt:TimerEvent):void
			{
				if(this.currentState=="main"||this.currentState=="fullscreen")
				{
					progressSlider.value=mc.currentFrame;
					if(progressSlider.value==progressSlider.maximum)
					{
						if(gotoComb.dataProvider==menuItemArray2 && this.currSectionNum!=this.totalSectionNum)
						{
							btnNext.dispatchEvent(new MouseEvent(MouseEvent.CLICK));
						}
						else
						{
							timer.stop();
						}
					}
					writeLog();
				}
				else
				{
					if(mc.currentFrame==mc.totalFrames)
					{
						if(this.currentState=="header")
						{
							btnBegin.visible=true;
							btnGoOn.visible=true;
							btnSkip.visible=false;
							timer.stop();
						}
						else
						{
							//btnSkip.label="开始学习";
							btnSkip.setStyle("upSkin",ksh_disabledSkin);
							btnSkip.setStyle("downSkin",ksh_overSkin);
							btnSkip.setStyle("overSkin",ksh_downSkin);
							btnSkip.setStyle("disabledSkin",ksh_upSkin);
							btnSkip.setStyle("right",10);
							btnSkip.setStyle("bottom",6);
						}
						
					}
				}
			}
			
			private function onMenuClick(evt:MouseEvent):void
			{	
				currChapterName=evt.target.data.fullName;		
				if(evt.target.data.xml!=null)
				{
					xml=evt.target.data.xml;
				}
				else
				{
					for each(var item:Object in menuItemArray1)
					{
						if(item.button==evt.target)
						{					
							currSectionNum=item.count;
							break;						
						}
					} 
					this.navFlag.visible=false;
					this.sectionName.visible=false;
				}				
				mainLoader.load(Button(evt.target).data.data);	
				if(lastBtn!=null)
				{
					lastBtn.setStyle("upSkin",upSkin);
				}
				Button(evt.target).setStyle("upSkin",focusSkin);
				lastBtn=evt.target as Button;									
			}
			
			/* private function onBtnFocusIn(evt:FocusEvent):void
			{
				Button(evt.target).setStyle("upSkin",focusSkin);	
			} 
			
			private function onBtnFocusOut(evt:FocusEvent):void
			{
				Button(evt.target).setStyle("upSkin",upSkin);
			}  */
			
			private function swfUnload():void
			{
				if(mc!=null)
				{
					mc.gotoAndStop(1);
					mc=null;
				}
			}
			
			private function loadComplete(evt:Event):void
			{
				if(xml==null)
				{
					if(gotoComb.dataProvider!=menuItemArray1)
					{
						gotoComb.dataProvider=menuItemArray1;
						gotoComb.labelField="count";					
				   		totalSectionNum=menuItemArray1.length;
					}
					try
					{
						mc=MovieClip(evt.target.content);
						if(isContinue)
						{
							mc.gotoAndStop(lsoInfo.currFrame);
							isContinue=false;
						}
					}
					catch(e:Error)
					{											
						Alert.show("SWF文件转换成MovieClip对象时出现问题,\n可能与flash发布版本有关.","提示");
					}			
					if(timer.running==false)
					{
						timer.start();
					}					
				}
			}
			
			private function multipageHandler(evt:Event):void
			{
				if(xml!=null)
				{
					evt.target.content.addEventListener(FlexEvent.APPLICATION_COMPLETE,appCompleteHandler);
				}
			}
			
			private function appCompleteHandler(evt:Event):void
			{	
				app=evt.target.application;				
				app.setApp(this);
				app.xmlUrl=this.xml;
				if(isContinue)
				{	
					app.isContinue=true;				
					app.currSectionNum=lsoInfo.currSectionNum-1;
					app.currFrame=lsoInfo.currFrame;
					this.isContinue=false;
				}				
				app.init();				
				this.xml=null;
				SystemManager(evt.target).removeEventListener(FlexEvent.APPLICATION_COMPLETE,appCompleteHandler);
			}
			
			public function innerSwfComplete():void
			{
				if(navFlag.visible==false)
				{
					this.navFlag.visible=true;
					this.sectionName.visible=true;
				}
				if(sectionName.visible==false)
				{
					this.sectionName.visible=true;
				}
				this.currSectionName=app.currCourse;
				this.currSectionNum=app.currPage;
				this.mc=app.mc;
				if(timer.running==false)
				{
					timer.start();
				}
			}
			
			public function innerArrayComplete():void
			{
				menuItemArray2=app.model;
				gotoComb.dataProvider=menuItemArray2;
				gotoComb.labelField="count";			
				totalSectionNum=menuItemArray2.length;
			}
			
			private function progressChange(evt:SliderEvent):void
			{
				
				if(timer.running==true)
				{
					if(mc!=null)
					{
						mc.gotoAndPlay(evt.value);
					}
				}
				else
				{
					if(mc!=null)
					{
						mc.gotoAndStop(evt.value);
					}
				}
			}
			
			private function play():void
			{
				if(timer.running==false)
				{
					timer.start();
				}
				if(mc!=null)
				{
					mc.play();
				}
			}
			
			private function pause():void
			{
				if(mc!=null)
				{
					mc.stop();
				}
				if(timer.running==true)
				{
					timer.stop();
				}
			}
			
			private function stop():void
			{
				if(timer.running==true)
				{
					timer.stop();
				}
				progressSlider.value=progressSlider.minimum;
				if(mc!=null)
				{
					mc.gotoAndStop(1);
				}
			}			
			
			private function prePage():void
			{
				if(gotoComb.dataProvider==menuItemArray2)
				{					
					var result:String=app.prePage();
					if(result=="first" && this.currChapterName!=menuItemArray1[0].button.fullName)
					{
						for(var i:int=1;i<menuItemArray1.length;i++)
						{
							if(this.currChapterName==menuItemArray1[i].button.fullName)
							{
								Button(menuItemArray1[i-1].button).dispatchEvent(new MouseEvent(MouseEvent.CLICK));
								break;
							}
						}
					}					
				}
				else
				{
					if(this.currSectionNum>1)
					{
						Button(menuItemArray1[currSectionNum-2].button).dispatchEvent(new MouseEvent(MouseEvent.CLICK));
					}
				}				
			}
			
			private function nextPage():void
			{
				if(gotoComb.dataProvider==menuItemArray2)
				{				
					var result:String=app.nextPage();
					if(result=="last" && this.currChapterName!=menuItemArray1[menuItemArray1.length-1].button.fullName)
					{
						for(var i:int=0;i<menuItemArray1.length-1;i++)
						{
							if(this.currChapterName==menuItemArray1[i].button.fullName)
							{
								Button(menuItemArray1[i+1].button).dispatchEvent(new MouseEvent(MouseEvent.CLICK));
								break;
							}
						}
					}					
				}
				else
				{
					if(this.currSectionNum<menuItemArray1.length)
					{
						Button(menuItemArray1[currSectionNum].button).dispatchEvent(new MouseEvent(MouseEvent.CLICK));
					}
				}				
			}
			
			private function gotoPage(evt:ListEvent):void
			{
				var item:Object=ComboBox(evt.target).selectedItem;				
				if(gotoComb.dataProvider==menuItemArray2)
				{
					app.gotoPage(ComboBox(evt.target).selectedIndex);
				}
				else
				{
					Button(item.button).dispatchEvent(new MouseEvent(MouseEvent.CLICK));
					/* mainLoader.source=item.data;
					callLater(mainLoader.load);//解决swf显示问题	 */
				}
				//gotoComb.selectedItem=null;		
			}
			
			private function fullscreen():void
			{
				this.currentState="fullscreen";
				this.stage.displayState=StageDisplayState.FULL_SCREEN;
			}
			
			private function normalState():void
			{
				this.stage.displayState=StageDisplayState.NORMAL;
				this.currentState="main";
			}
			
			private function columeControlHandler():void
			{
				if(columeControl.toolTip=="关闭")
				{
					columeControl.toolTip="打开";
					columeControl.setStyle("upSkin",sy_downSkin);
					columeControl.setStyle("overSkin",sy_downSkin);
					soundTrans.volume=0;
					SoundMixer.soundTransform=soundTrans;
				}
				else
				{
					columeControl.toolTip="关闭";
					columeControl.setStyle("upSkin",sy_upSkin);
					columeControl.setStyle("overSkin",sy_overSkin);
					columeControl.setStyle("downSkin",sy_downSkin);
					soundTrans.volume=columeSlider.value;
					SoundMixer.soundTransform=soundTrans;
				}
			}
			
			private function columeChange(evt:SliderEvent):void
			{
				if(columeControl.toolTip=="关闭")
				{
					soundTrans.volume=evt.value;
					SoundMixer.soundTransform=soundTrans;
				}
			}
			
			private function formatProgressToolTip(value:int):String
    		{
    			value=Math.ceil(value / 12);  //flash 12frames/s  			
    			var result:String = (value % 60).toString();
      		    if (result.length == 1)
      		    {
         			result = Math.floor(value / 60).toString() + ":0" + result;
       		 	}
      			else
			  	{
       		    	result = Math.floor(value / 60).toString() + ":" + result;
       		  	}      			      			 
      		    return result;
  			} 
  			
  			private function formatColumeToolTip(value:Number):String
  			{
  				var temp:String=value.toString();
  				if(temp.length==1)
  				{
  					return temp+".0";
  				}
  				else
  				{
  					return temp.substr(0,3);
  				}
  			}
  			//Flash Player 会在共享对象会话结束时（也就是说，在 SWF 文件关闭时，在由于不再有对共享对象的任何引用而将其作为垃圾回收时，或者在调用 SharedObject.clear() 或 SharedObject.close() 时），将共享对象写入文件
  			private function writeLog():void
  			{
  				lastCourseInfo.isMultipage=(gotoComb.dataProvider==menuItemArray2)?true:false;
  				lastCourseInfo.currChapterName=this.currChapterName;
  				lastCourseInfo.currSectionNum=this.currSectionNum;
  				lastCourseInfo.currFrame=mc.currentFrame;  				
  			}
  			
  			private function note():void
  			{
  				if(isPopup)
  				{
  					PopUpManager.removePopUp(noteManage);
  					isPopup=false; 
  				}
  				else
  				{
  					pause();
  					noteManage=new NoteManage(); 
  					noteManage.setApp(this);
  					var point:Point=new Point();								
					point=mainLoader.localToGlobal(point);
  					noteManage.x=point.x;
  					noteManage.y=point.y; 
  					PopUpManager.addPopUp(noteManage,this);
  					isPopup=true; 
  				}
  			}
  			
			private function help():void
			{
				navigateToURL(new URLRequest(content.Help.@data));//默认:"_blank"
			}
				
  			private function exit():void
			{//一些浏览器不支持通过 navigateToURL() 方法使用 javascript 协议。而应考虑使用 ExternalInterface API 的 call() 方法在包含该内容的 HTML 页中调用 JavaScript 方法。
				navigateToURL(new URLRequest("javascript:window.close()"),"_self");//"window.opener=null"使关闭窗口时不弹出确认信息窗口
			}
			
			private function skip():void
			{
				if(this.currentState=="header")
				{
					mc.gotoAndStop(mc.totalFrames);
					btnBegin.visible=true;
					btnGoOn.visible=true;
					btnSkip.visible=false;
				}
				else
				{
					mc.gotoAndStop(1);
					init();
				}
			}
			
			private function headerLoadComp():void
			{
				mc=MovieClip(headLoader.content);
				timer.start();
			}
			private function headLoaderUnload():void
			{
				mc.gotoAndStop(1);
			}
			private function beginStudy():void
			{
				this.currentState="begin";
				headLoader.source=content.Begin.@data;
				btnSkip.visible=true;
			}
			private function goOnStudy():void
			{
				this.isContinue=true;
				init();
			}
			private function sliderComplete(evt:Event):void
			{
				 var slider:HSlider = evt.target as HSlider;
				 var thumb:SliderThumb = slider.getThumbAt(0);
				 thumb.width = 36.3;
				 thumb.height = 7;
				 //thumb.xPosition=18;
				 slider.invalidateDisplayList();
			}
			private function lbMouseOver1():void
			{
				btnPreviouse.setStyle("textDecoration","normal");				
			}
			private function lbMouseOver2():void
			{
				btnNext.setStyle("textDecoration","normal");
			}
			private function lbMouseOut1():void
			{
				btnPreviouse.setStyle("textDecoration","underline");
			}
			private function lbMouseOut2():void
			{
				btnNext.setStyle("textDecoration","underline");
			}
		]]>
	</mx:Script>	
	
	<mx:states>
		<mx:State name="main">
			<mx:AddChild position="lastChild">
				<mx:Canvas height="550" width="800" backgroundImage="@Embed(source='images/background.jpg')" horizontalCenter="0" verticalCenter="0" id="mainCanvas">
					<mx:Image x="26" y="25" id="logoImg" source="{logo}"/>						
					<mx:Image x="300" y="23" id="courseNameLImg" source="{courseNameL}"/>
					<mx:Button x="627" y="37" label="笔记" width="51" height="25" id="button7" click="note()" fontWeight="normal" styleName="button" cornerRadius="3"/>
					<mx:Button x="681" y="37" label="帮助" width="51" height="25" id="button8" click="help()" fontWeight="normal" styleName="button" cornerRadius="3"/>
					<mx:Button x="735" y="37" label="退出" width="51" height="25" id="button9" click="exit()" fontWeight="normal" styleName="button" cornerRadius="3"/>
					
					<mx:Image x="8" y="81" id="menuTitleImg" source="{menuTitle}"/>				
					<mx:Grid id="gridMenu" x="4.5" y="128" width="127" height="338" horizontalGap="1" verticalGap="1.5"/>
					<mx:Image x="6" y="526" id="courseNameSImg" source="{courseNameS}"/>
											
					<mx:HBox x="133" y="72" width="654" height="26" verticalAlign="middle" horizontalGap="3" id="hbox1" color="#FFFFFF">
						<mx:Spacer width="20"/>
						<mx:Label text="{currChapterName}" id="chapterName"/>
						<mx:Label id="navFlag" text="-&gt;" fontFamily="Courier New"/>
						<mx:Label text="{currSectionName}" id="sectionName"/>
					</mx:HBox>					
					<mx:SWFLoader id="mainLoader" init="multipageHandler(event)" complete="loadComplete(event)" unload="swfUnload()" scaleContent="true" autoLoad="false" x="133" y="99" height="389" width="652" horizontalAlign="center" verticalAlign="middle"/>
					<mx:Button label="全屏显示" width="62" height="20" x="715" y="462.5" id="button5" click="fullscreen()" fontSize="12" fontWeight="normal"/>
					
					<mx:HSlider x="148" y="488" width="620" height="7" id="progressSlider" creationComplete="sliderComplete(event)" liveDragging="true" minimum="0" maximum="{mc.totalFrames}" snapInterval="1" dataTipFormatFunction="formatProgressToolTip" change="progressChange(event)" thumbPress="pause()" thumbRelease="play()" buttonMode="true" useHandCursor="true" styleName="progressSlider" showTrackHighlight="true" trackHighlightSkin="{component.SliderHighlightSkin}" slideDuration="140"/>
					<mx:Button x="153" y="506" width="22" id="button3" click="play()" styleName="playButton" toolTip="播放"/>
					<mx:Button x="180" y="506" width="22" id="button2" click="pause()" styleName="pauseButton" toolTip="暂停"/>
					<mx:Button x="207" y="506" width="22" id="button1" click="stop()" styleName="stopButton" toolTip="停止"/>
						<mx:LinkButton x="269" y="507" label="前一页" id="btnPreviouse" click="prePage()" mouseOver="lbMouseOver1()" mouseOut="lbMouseOut1()" alpha="0.0" textDecoration="underline"/>
						<mx:Label x="326" y="509" text="当前页" id="label1"/>
						<mx:Label x="365" y="510" text="{currSectionNum}" textAlign="center" id="label3"/>
						<mx:Label x="389" y="509" text="总页数" id="label2"/>
						<mx:Label x="430" y="510" text="{totalSectionNum}" textAlign="center" id="label4"/>
						<mx:LinkButton x="450" y="507" label="后一页" id="btnNext" click="nextPage()" mouseOver="lbMouseOver2()" mouseOut="lbMouseOut2()" alpha="0.0" textDecoration="underline"/>
						<mx:Label x="512" y="509" text="直接跳转到" id="label5"/>
						<mx:ComboBox x="580" y="507.5" width="49" id="gotoComb" change="gotoPage(event)" labelField="count" fontSize="10" cornerRadius="3"/>
						<mx:Label x="632" y="509" text="页" textAlign="center" id="label6"/>
							
						<mx:Button x="678" y="506" id="columeControl" click="columeControlHandler()" styleName="soundButton" upSkin="{sy_upSkin}" overSkin="{sy_overSkin}" downSkin="{sy_downSkin}" toolTip="关闭"/>						
						<mx:HSlider x="703" y="504" width="62" id="columeSlider" minimum="0" maximum="1" value="0.6" liveDragging="true" change="columeChange(event)" dataTipFormatFunction="formatColumeToolTip" styleName="columeSlider" height="11"/>
				</mx:Canvas>
			</mx:AddChild>			
		</mx:State>
		<mx:State name="fullscreen" basedOn="main">	
			<mx:RemoveChild target="{logoImg}"/>
			<mx:RemoveChild target="{courseNameLImg}"/>
			<mx:RemoveChild target="{menuTitleImg}"/>
			<mx:RemoveChild target="{courseNameSImg}"/>		
			<mx:RemoveChild target="{button7}"/>
			<mx:RemoveChild target="{button8}"/>
			<mx:RemoveChild target="{button9}"/>
			<mx:RemoveChild target="{gridMenu}"/>
			<mx:RemoveChild target="{hbox1}"/>			
			<mx:RemoveChild target="{button3}"/>
			<mx:RemoveChild target="{button2}"/>
			<mx:RemoveChild target="{button1}"/>
			<mx:RemoveChild target="{progressSlider}"/>			
			<mx:RemoveChild target="{btnPreviouse}"/>
			<mx:RemoveChild target="{label1}"/>
			<mx:RemoveChild target="{label3}"/>
			<mx:RemoveChild target="{label2}"/>
			<mx:RemoveChild target="{label4}"/>
			<mx:RemoveChild target="{btnNext}"/>
			<mx:RemoveChild target="{label5}"/>
			<mx:RemoveChild target="{gotoComb}"/>
			<mx:RemoveChild target="{label6}"/>
			<mx:RemoveChild target="{columeControl}"/>
			<mx:RemoveChild target="{columeSlider}"/>
			<mx:RemoveChild target="{button5}"/>
			<mx:SetProperty target="{mainLoader}" name="width" value="100%"/>
			<mx:SetProperty target="{mainLoader}" name="x"/>
			<mx:SetProperty target="{mainLoader}" name="height" value="100%"/>
			<mx:SetProperty target="{mainLoader}" name="y"/>
			<mx:SetProperty target="{mainCanvas}" name="width" value="100%"/>
			<mx:SetProperty target="{mainCanvas}" name="height" value="100%"/>			
			<mx:AddChild relativeTo="{mainCanvas}" position="lastChild">
				<mx:Button label="退出全屏" right="35" top="20" height="22" click="normalState()" styleName="button"/>
			</mx:AddChild>
			<mx:SetStyle name="horizontalAlign" value="center"/>
			<mx:SetStyle name="verticalAlign" value="middle"/>
			<mx:SetProperty name="width" value="100%"/>
			<mx:SetProperty name="height" value="100%"/>			
			<mx:SetProperty name="layout" value="absolute"/>
			<mx:SetProperty target="{mainLoader}" name="width" value="100%"/>
			<mx:SetProperty target="{mainLoader}" name="height" value="100%"/>			
			<mx:SetStyle target="{mainLoader}" name="horizontalCenter" value="0"/>			
			<mx:SetStyle target="{mainLoader}" name="verticalCenter" value="0"/>			
			<mx:SetStyle target="{mainCanvas}" name="backgroundImage"/>
			<mx:SetProperty target="{mainCanvas}" name="x" value="0"/>
			<mx:SetStyle target="{mainCanvas}" name="horizontalCenter"/>
			<mx:SetProperty target="{mainCanvas}" name="y" value="0"/>
			<mx:SetStyle target="{mainCanvas}" name="verticalCenter"/>
		</mx:State>
		<mx:State name="header">
			<mx:AddChild position="lastChild">
				<mx:Canvas width="585" height="389" verticalCenter="0" horizontalCenter="0" id="canvas1">
					<mx:SWFLoader source="{content.Header.@data}" width="100%" height="100%" horizontalCenter="0" verticalCenter="0" id="headLoader" scaleContent="true" complete="headerLoadComp()" unload="headLoaderUnload()"/>
					<mx:Button id="btnSkip" click="skip()" styleName="skipButton" right="0" bottom="0"/>
					<mx:Button id="btnBegin" left="200" y="220" visible="false" click="beginStudy()" styleName="beginButton"/>
					<mx:Button id="btnGoOn" right="200" y="220" visible="false" click="goOnStudy()" styleName="goOnButton"/>
				</mx:Canvas>
			</mx:AddChild>			
		</mx:State>
		<mx:State name="begin" basedOn="header">
			<mx:RemoveChild target="{btnBegin}"/>
			<mx:RemoveChild target="{btnGoOn}"/>
			<mx:SetProperty target="{canvas1}" name="width" value="800"/>
			<mx:SetProperty target="{canvas1}" name="height" value="550"/>
		</mx:State>
	</mx:states>	
</mx:Application>
